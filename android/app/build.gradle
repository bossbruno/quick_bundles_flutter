plugins {
    id "com.android.application"
    // START: FlutterFire Configuration
    id 'com.google.gms.google-services'
    // END: FlutterFire Configuration
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

configurations.all {
    resolutionStrategy {
        force 'androidx.webkit:webkit:1.7.0'
    }
}

// Load environment variables from .env file
def env = new HashMap<String, String>()
file("${rootProject.projectDir}/../.env").readLines().each { line ->
    if (!line.trim().isEmpty() && !line.trim().startsWith('#')) {
        def (key, value) = line.split('=', 2).collect { it.trim() }
        if (key && value) {
            env.put(key, value.replace('"', '').replace('\'', ''))
        }
    }
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "com.theden.quickbundles"
    compileSdkVersion 35
    ndkVersion "25.1.8937393"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.theden.quickbundles"
        minSdkVersion 23
        targetSdkVersion 35
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
        
        // Set Firebase configuration from environment variables
        manifestPlaceholders = [
            applicationName: 'android.app.Application',
            firebaseApiKey: env.FIREBASE_API_KEY ?: "",
            firebaseAppId: env.FIREBASE_APP_ID ?: "",
            firebaseProjectId: env.FIREBASE_PROJECT_ID ?: "",
            firebaseStorageBucket: env.FIREBASE_STORAGE_BUCKET ?: "",
            firebaseMessagingSenderId: env.FIREBASE_MESSAGING_SENDER_ID ?: "",
            firebaseMeasurementId: env.FIREBASE_MEASUREMENT_ID ?: ""
        ]
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            // TODO: Add your own signing config for the release build.
            // Signing with the debug keys for now, so `flutter run --release` works.
            //signingConfig signingConfigs.debug
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            shrinkResources false
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        checkReleaseBuilds false
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation platform('com.google.firebase:firebase-bom:33.14.0')
    implementation("com.google.firebase:firebase-firestore")
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
}


